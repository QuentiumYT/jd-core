plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://plugins.gradle.org/m2/'
    }
}

dependencies {
    testImplementation 'commons-codec:commons-codec:1.13'
    testImplementation 'org.apache.commons:commons-collections4:4.1'
    testImplementation 'org.apache.commons:commons-imaging:1.0-alpha1'
    testImplementation 'org.apache.commons:commons-lang3:3.9'
    testImplementation 'com.jakewharton:disklrucache:2.0.2'
    testImplementation 'com.squareup:javapoet:1.11.1'
    testImplementation 'com.squareup:javawriter:2.5.1'
    testImplementation 'joda-time:joda-time:2.10.5'
    testImplementation 'org.joda:joda-convert:2.2.1'
    testImplementation 'org.jsoup:jsoup:1.12.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'javax.jms:javax.jms-api:2.0.1'
    testImplementation 'javax.mail:javax.mail-api:1.6.2'
    testImplementation 'com.squareup.mimecraft:mimecraft:1.1.1'
    testImplementation 'org.scribe:scribe:1.3.7'
    testImplementation 'com.sparkjava:spark-core:2.9.1'
    testImplementation 'log4j:log4j:1.2.17'
    testImplementation 'com.google.guava:guava:12.0'
}

version = '1.2.3'

jar {
    manifest {
        attributes 'JD-Core-Version': version
    }
}

// Java compilation settings
tasks.withType(JavaCompile) {
    sourceCompatibility = targetCompatibility = '1.8'
    options.compilerArgs << '-Xlint:deprecation'
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:-options'
    options.encoding = 'UTF-8'
}

// Publication to Maven repository
task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

// Publication to Maven Local repository
publishing {
    publications {
        publicationJdCore(MavenPublication) {
            groupId = 'org.jd'
            artifactId = 'jd-core'
            version = project.version
            from components.java
            artifact tasks.sourceJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/QuentiumYT/jd-core")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
            }
        }
        mavenLocal()
    }
}
